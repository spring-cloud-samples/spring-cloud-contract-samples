buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: 'org.asciidoctor.convert'

repositories {
    maven { url "https://repo.spring.io/release" }
}

wrapper {
    gradleVersion '3.5'
}

dependencies {
    asciidoctor "io.spring.asciidoctor:spring-asciidoctor-extensions:0.1.1.RELEASE"
}

// Documentation tasks
asciidoctor {
    sourceDir = file('docs')
    sources {
        include 'index.adoc', 'workshops.adoc', 'tutorials/*'
    }
    outputDir = file('docs')
    separateOutputDirs = false
    resources { }
    attributes 'source-highlighter': 'prettify'

}

task removeHtml() {
  doLast {
      FileTree tree = fileTree(dir: 'docs', include: ['**/*.html'])
      tree.each {
        it.delete()
      }
  }
}

asciidoctor.mustRunAfter removeHtml

task generateDocumentation(dependsOn: ["removeHtml", "asciidoctor"]) {
    group = "Documentation"
    description = "Creates full documentation for the project"
}

// Workshop tasks
task removeFiles(type: Delete) {
    group = "Workshops"
    description = "Removes all contracts"
    List<String> producers =  project.rootDir.list(new FilenameFilter() {
        @Override
        public boolean accept(File current, String name) {
          return new File(current, name).isDirectory() && name.startsWith("producer")
        }
      })
    List<String> folders = producers.collect {
        "${it}/src/test/resources/contracts/"
    }
    List<String> adocTests = producers.collect {
        "${it}/src/test/java/com/example/GenerateAdocsFromContractsTests.java"
    }
    List<String> graphsTests = producers.collect {
        "${it}/src/test/java/com/example/GenerateGraphFromContractsTests.java"
    }
    delete folders
    delete adocTests
    delete graphsTests
    delete 'beer_contracts/src/main/resources/contracts/'
    delete 'beer_contracts/src/test/java/docs/GenerateAdocsFromContractsTests.java'
    delete 'beer_contracts/src/test/java/docs/GenerateGraphFromContractsTests.java'
}

task removeTextFromFiles() {
    group = "Workshops"
    description = "Removes written implementation"
    doLast {
        FileTree tree = fileTree(dir: '.', include: ['**/*.java',
                                                     '**/*.groovy',
                                                     '**/*.gvy',
                                                     '**/*.xml',
                                                     '**/*.gradle',
                                                     '**/*.yml',
                                                     '**/*.properties'])
        tree.each { File file ->
            if (file.absolutePath == new File(project.rootDir, "build.gradle").absolutePath) {
                return
            }
            String text = file.text
            if (!text.contains("remove::start")) {
                return
            }
            StringBuilder newString = new StringBuilder()
            boolean remove = false
            text.eachLine { String line ->
                if(["import org.springframework.cloud.contract", "tag::", "end::"].any { line.contains(it) }) {
                    return
                }
                if (line.contains("remove::end[]")) {
                    remove = false
                } else if (line.contains("remove::end[return]")) {
                    // only related to java
                    newString.append(line.replace("//remove::end[return]", "return null;")).append("\n")
                    remove = false
                    return
                } else if (line.contains("//@org.junit.Ignore")) {
                    // only related to java
                    newString.append(line.replace("//", "")).append("\n")
                    return
                }
                if (!remove) {
                    if (line.contains("remove::start")) {
                        remove = true
                    } else if (!line.contains("remove::end")) {
                        newString.append(line).append("\n")
                    }
                }
            }
            file.text = newString.toString()
        }
    }
}

task prepareForWorkshops(dependsOn: ['removeFiles', 'removeTextFromFiles']) {
    group = "Workshops"
    description = "Executes all tasks to set initial state for workshops"
}
