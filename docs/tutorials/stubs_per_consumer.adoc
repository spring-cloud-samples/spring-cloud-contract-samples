:toc: left
:toclevels: 8
:nofooter:
:root: ../..
:consumer: {root}/consumer_with_stubs_per_consumer
:producer: {root}/producer_with_stubs_per_consumer
:images_folder: ../images
:where_are_contracts: the producer's code
:producer_artifact: beer-api-producer-with-stubs-per-consumer
:version: 1.2.0.BUILD-SNAPSHOT

= Contracts on the Producer Side {version}

In this tutorial, we keep the contracts together with the producer code. Each consumer
defines the contracts in a dedicated folder. For the same requests, the consumers expect
different responses.

== Scenarios

We must write the following features:

image::{images_folder}/scenario_stubs_per_consumer_1.png[title="Unofficially addresses the person by name for positive beer selling via HTTP"]

{nbsp}
{nbsp}

image::{images_folder}/scenario_stubs_per_consumer_2.png[title="Unofficially addresses the person by name for negative beer selling via HTTP"]

{nbsp}
{nbsp}

image::{images_folder}/scenario_stubs_per_consumer_3.png[title="Officially addresses the person by surname for positive beer selling via HTTP"]

{nbsp}
{nbsp}

image::{images_folder}/scenario_stubs_per_consumer_4.png[title="Officially addresses the person by surname for negative beer selling via HTTP"]

{nbsp}
{nbsp}

== Flow

image::{images_folder}/flow.png[title="Consumer Driven Contract Flow"]

== Tutorial

Consumer Driven Contracts are like TDD in terms of architecture. We start by writing a
test on the consumer side. This time, we simulate in our single code base as if we had
two separate consumers. Normally, both consumers would have their own code bases, but,
for the sake of demonstration, we try to keep things simple.

=== Consumer flow 1

image::{images_folder}/consumer_flow_1.png[title="Interact with cloned producer code"]

==== IDE setup

* In your IDE, open the `consumer_with_stubs_per_consumer` project (either via Maven or
Gradle)
* We have the following objectives for HTTP:
** As a consumer with name `foo-service`:
*** For a client with a certain `name` and `age`, we ask the producer to verify whether
the person is eligible to get the beer.
*** We expect to retrieve the `status` and the `name` of the client.
*** Depending on the `status`, we either accept or decline giving a beer.
*** Since we have a friendly service, we address the client by `MY DEAR FRIEND` and then
use the person's `name`.
*** If the person (for example, with the name, `marcin`) can get the beer then we say
`THERE YOU GO MY DEAR FRIEND [marcin]`.
*** Otherwise, we say `GET LOST MY DEAR FRIEND [marcin]`.
** As a consumer with name `bar-service`:
*** For the client with a certain `name` and `age`, we ask the producer to verify whether
the person is eligible to get the beer.
*** We expect to retrieve the `status` and the `surname` of the client (for simplicity we
receive the provided `name` as `surname`).
*** Depending on the `status`, we either accept or decline giving a beer.
*** Since we have a very official service, we address the client by `MR`.
*** If the person (for example, with the name, `marcin`) can get the beer, then we say
`THERE YOU GO MR [marcin]`, Where `marcin` came back from the producer service as a
`surname`.
*** Otherwise, we say `GET LOST MR [marcin]`.
* Normally we would do TDD, but we already have some code ready to speed things up.
* In the `BeerController` class, you can see that the implementation of the method is
missing. We return to that later. For now, you can see that we have a `/beer` endpoint
that expects a JSON request body that maps to a `Person` class.
* Now open the `BeerControllerTest` and write the missing test bodies.

[source,java]
----
include::{root}/consumer_with_stubs_per_consumer/src/test/java/com/example/BeerControllerTest.java[tags=impl]
----
We need to name our consumer somehow. The best way is to provide that value in the
`properties` attribute in a `SpringBootTest` annotation, though you also could pass it
via a file, such as `application.yml`.

[source,java]
----
include::{root}/consumer_with_stubs_per_consumer/src/test/java/com/example/BeerControllerTest.java[tags=foo]
----

If we run the tests, they fail, because we have no implementation.
Now open `BeerControllerForBarTest`. In this test class, we simulate that we are using
the `bar-service` and not the `foo-service`. We can start with the missing test
implementation.

[source,java]
----
include::{root}/consumer_with_stubs_per_consumer/src/test/java/com/example/BeerControllerForBarTest.java[tags=impl]
----
In this test, we do not set the `spring.application.name`. We will change the name with
an attribute in an annotation later.
Now we want to write an implementation, but the problem is that we do not yet know what
API we would like to have. Here, we touch the very essence of Consumer Driven
Contracts. As consumers, we want to drive the change of the API. That is why, *as
consumers*, we work on the producer code.

include::snippets/setup_cloned_producer.adoc[]

==== Defining first `foo-consumer` HTTP contract

It is time to play with the API Create a `src/test/resources/contracts/foo-consumer/rest`
folder. You can define the contracts using Groovy DSL. To create your first HTTP
contract:

. Under the `rest` folder, create a file called `shouldGrantABeerIfOldEnough.groovy`
. Call the `Contract.make` method to start defining the contract.

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {

}
----

You can call `description()` method to provide some meaningful description.

TIP: You can use the Groovy multiline String `""" """` to have all special characters
escaped. Every new line in the String is converted into a new line character, as shown in
the following example:

[source,groovy]
----
Contract.make {
	request {
		description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
""")
}
----

Now call the `request { }` and `response { }` methods, as shown in the following example:

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
        some interesting description
    """)
    request {
    }
    response {
    }
}
----

Let's assume that we want to send a `POST` method. To do so, call `method POST()` or
`method "POST"`.

TIP: In Groovy, you do not need to provide parentheses (in most cases). You can write
either `method POST()` or `method(POST())`. The result is the same.

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
        some interesting description
    """)
    request {
        method POST()
    }
    response {
    }
}
----

Now we need to provide a URL: `/check`. We can write `url "/check"`.

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
        some interesting description
    """)
    request {
        method POST()
        url "/check"
    }
    response {
    }
}
----

* Now we need to define the body. We leverage some of Groovy's power here, so, if you
get lost you can always check the http://groovy-lang.org/json.html[Groovy JSON
documentation]. Let's call the `body()` method with brackets.
// TODO It'd be good to show calling the body with brackets.
* In Groovy, you can use the map notation this way: `[key: "value", secondKey: 2]`. In
the same way, we can describe the body with JSON. We want to send JSON such as the
following `{ "age": 22, "name": "marcin" }`. To do so, we can create a map notation of
`[age:22, name:"marcin"]`. The `body` method accepts a map. In Groovy, if a method
accepts a map, then the `[]` brackets can be omitted. So you can write either
`body([age:22, name:"marcin"])` or `body(age:22, name:"marcin")`.
* Let's assume that we already want to be more generic about our contract and we want to
verify that the age is greater than 20 and that name is any alphaunicode character. We
can use the `$()` or `value()` methods that Spring Cloud Contract provides to define
dynamic behaviour.
* We use the `$(regex(...))` method to define the age and the `$(anyAlphaUnicode())` for
the name.

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
    }
    response {
    }
}
----

Now we can work on the headers, by calling the `headers { }` method, as shown in the
following example:

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {

        }
    }
    response {
    }
}
----

Inside that method, we want to use the `Content-Type: "application/json` header. To do
so, call `contentType(applicationJson())` methods, as shown in the following example:

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
    }
}
----

Congratulations! You defined the contract for the request. Now we can work on the
response

In the `response` block, we want to define that the status of our response will be 200.
To do so, call `status 200`, as shown in the following example:
+
[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
    }
}
----

* We want our response to have a body. As you might have guessed, there's a `body` method
here, too. We can now use another way of defining bodies by using String. (That is the
less preferred option in Spring Cloud Contract, but it can still be useful.)
* We want to send back a field called `status` that will return `OK` when the person can
get the beer. The `foo-consumer` is also interested in getting the name in the response
from the request.
** To reference the request from the response via the JSON path, you can call the
`fromRequest()` method. In the following code snippet, we reference the `name` field from
the request: `fromRequest().body('$.name')".
** In Groovy, when you use a multiline string (`""" """`), you can call the `${}`
interpolation mechanism to call a method from within a String.

TIP: Don't confuse the `$()` from Spring Cloud Contract with the `${}` interpolation
mechanism. Call
`body(""" { "status" : "OK", "name": "${fromRequest().body('$.name')}" } """)`. That way,
you can define how the response body looks by providing the exact JSON value, and inside
that JSON you can also provide dynamic values. In our case, for the `name` response
field, we provide the value of `name` from the request, as shown in the following
example:

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
        body("""
            {
                "status" : "OK",
                "name": "${fromRequest().body('$.name')}"
            }
        """)
    }
}
----

The last thing to add is the response headers. We do just about exactly the same thing as
we did previously for the request, as shown here:
`headers { contentType(applicationJson()) }`.

[source,groovy]
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
        body("""
            {
                "status" : "OK",
                "name": "${fromRequest().body('$.name')}"
            }
        """)
        headers {
            contentType(applicationJson())
        }
    }
}
----

Congratulations! You have created your first contract!

==== Defining the Second `foo-consumer` HTTP Contract

Now it's time for you to create the second contract. Create a file called
 `shouldRejectABeerIfTooYoung.groovy`.
If you get lost, look at the <<_foo_consumer_contracts,solution>>.

. Set the `age` in the request to the following regular expression `[0-1][0-9]`.
. Update the response body to return a `status` of `NOT_OK`.
. Update the description.

==== Defining `bar-consumer` HTTP contracts

Let's now move to the `bar-consumer`. Create a
`src/test/resources/contracts/bar-consumer/rest` folder. We will create very similar
contracts to the `foo-consumer` one. The only difference is that the response contains a
`surname` field instead of a `name` field. Create 2 files named
`shouldGrantABeerIfOldEnough.groovy` and `shouldRejectABeerIfTooYoung.groovy` and fill
them out <<_bar_consumer_contracts,or copy from the solution>>. We have written the
contracts. It is time to publish some stubs!

include::snippets/setting_plugin_on_clone.adoc[]

==== Writing missing implementation on the consumer side

We know what the API should look like. Let's go to `BeerController` and write the
missing implementation <<_stubs_per_consumer_beercontroller,or copy from the solution>>.

* We want to send a POST HTTP method to `http://localhost:8090/check`.
* The JSON body will contain the `Person` that we received in the controller.
* Depending on the status (`OK` or `NOT_OK`), we send back:
** For `OK`: `THERE YOU GO ` + the result of the `message(body of the response)` method.
** For `NOT_OK`: `GET LOST ` + the result of the `message(body of the response)` method.

If we run the `BeerControllerTest` and `BeerControllerForBarTest`, they both fail due
to the connection being refused. Let's fix that.

==== Turning on Stub Runner in Consumer Tests

Since we managed to install the stubs locally and we now have the missing implementation
written, we can now go back to the consumer tests. Let's add the Spring Cloud Contract
Stub Runner as a dependency, as shown in the following example:

include::snippets/consumer_stub_runner_dep.adoc[]

Let's check out the `BeerControllerTest` and add the Stub Runner functionality, as shown
in the following example:

[source,java]
----
@AutoConfigureStubRunner(workOffline = true,
		ids = "com.example:beer-api-producer-with-stubs-per-consumer",
		stubsPerConsumer = true)
----

You can see that we turned on the `stubsPerConsumer` flag. Doing so means that the path
of stubs is scanned and only those that contain the value of `spring.application.name`
is picked. Now let's run the test. It should pass. Let's try to fix the
`BeerControllerForBarTest`. We do not want to set the `spring.application.name`. That's
why we will set that name on the `@AutoConfigureStubRunner` annotation via the
`consumerName` property. (Note that we also have to turn on the `stubsPerConsumer` flag.)

[source,java]
----
@AutoConfigureStubRunner(workOffline = true,
		ids = "com.example:beer-api-producer-with-stubs-per-consumer",
		stubsPerConsumer = true,
		consumerName = "bar-consumer")
----

Congratulations! As a consumer, you successfully used the API of the producer for both
HTTP and messaging. Now we can file a pull request (PR) to their code to propose a
contract. Let's switch to the producer side.

=== Producer flow 1

image::{images_folder}/producer_flow_1.png[title="Producer takes over the PR, writes missing impl and merges the PR"]

include::snippets/producer_ide_setup.adoc[]

include::snippets/setting_plugin_on_producer.adoc[]

In our situation, we use the mapping approach. Let's set the following base classes for
our contracts, as shown in the following example:

[source,xml,indent=0,subs="verbatim,attributes",role="primary"]
.Maven
----
<plugin>
    <groupId>org.springframework.cloud</groupId>
    <artifactId>spring-cloud-contract-maven-plugin</artifactId>
    <version>${spring-cloud-contract.version}</version>
    <extensions>true</extensions>
    <configuration>
        <baseClassMappings>
            <baseClassMapping>
                <contractPackageRegex>.*rest.*</contractPackageRegex>
                <baseClassFQN>com.example.BeerRestBase</baseClassFQN>
            </baseClassMapping>
        </baseClassMappings>
    </configuration>
</plugin>
----

[source,groovy,indent=0,subs="verbatim,attributes",role="secondary"]
.Gradle
----
contracts {
	baseClassMappings {
		baseClassMapping(".*rest.*", "com.example.BeerRestBase")
	}
}
----

include::snippets/generating_tests_from_contract.adoc[]

==== Fixing broken HTTP tests

Let's start with HTTP. First, let's write the missing implementation in
`ProducerController`. The logic to be written is as follows: If the
`personCheckingService.shouldGetBeer(...)` returns `true`, then we should return
`new Response(BeerCheckStatus.OK, personToCheck.name)`. Otherwise, we should return
`new Response(BeerCheckStatus.NOT_OK, personToCheck.name)`.
(<<_producerController_for_stubs_per_consumer_implementation,Show solution>>).

include::snippets/fixing_base_class.adoc[]

Now you can merge the pull request to `master` and your CI system can build a fat jar and
the stubs.

IMPORTANT: Per consumer stubs is a powerful feature. On the producer side, if you want to
remove a field from the response, you can quickly verify if gets used. Try removing
the `surname` field from the `Response` class. You can see that the generated `RestTest`
in the `bar-consumer` subfolder fails. That means that the `bar-consumer` requires the
`surname` field and that you can't safely remove it. On the other hand, in production,
both consumers receive more fields than they define in the contract. Thus, if they do not
configure their serializers properly (to ignore unknown fields), then their tests pass
but their integrations fail.

Congratulations! You have completed the producer side of this tutorial.

include::snippets/consumer_flow_2.adoc[]

include::snippets/generating_docs.adoc[]

== Solutions

include::snippets/solutions.adoc[]

=== Foo-consumer contracts

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.Successful
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
        body("""
            {
                "status" : "OK",
                "name": "${fromRequest().body('$.name')}"
            }
        """)
        headers {
            contentType(applicationJson())
        }
    }
}
----

[source,groovy,indent=0,subs="verbatim,attributes",role="secondary"]
.Unsuccessful
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents an unsuccessful scenario of getting a beer

```
given:
	client is too young
when:
	he applies for a beer
then:
	we'll NOT grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[0-1][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
        body("""
            {
                "status" : "NOT_OK",
                "name": "${fromRequest().body('$.name')}"
            }
        """)
        headers {
            contentType(applicationJson())
        }
    }
}
----

=== Bar-consumer contracts

[source,groovy,indent=0,subs="verbatim,attributes",role="primary"]
.Successful
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents a successful scenario of getting a beer

```
given:
	client is old enough
when:
	he applies for a beer
then:
	we'll grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[2-9][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
        body("""
            {
                "status" : "OK",
                "surname": "${fromRequest().body('$.name')}"
            }
        """)
        headers {
            contentType(applicationJson())
        }
    }
}
----

[source,groovy,indent=0,subs="verbatim,attributes",role="secondary"]
.Unsuccessful
----
org.springframework.cloud.contract.spec.Contract.make {
    description("""
Represents an unsuccessful scenario of getting a beer

```
given:
	client is too young
when:
	he applies for a beer
then:
	we'll NOT grant him the beer
```
    """)
    request {
        method POST()
        url "/check"
        body(
                age: $(regex("[0-1][0-9]")),
                name: $(anyAlphaUnicode())
            )
        headers {
            contentType(applicationJson())
        }
    }
    response {
        status 200
        body("""
            {
                "status" : "NOT_OK",
                "surname": "${fromRequest().body('$.name')}"
            }
        """)
        headers {
            contentType(applicationJson())
        }
    }
}
----

=== Stubs Per Consumer BeerController

[source,java]
----
include::../../consumer_with_stubs_per_consumer/src/main/java/com/example/BeerController.java[tags=impl]
----

=== ProducerController for stubs per consumer implementation

[source,java]
----
if (personCheckingService.shouldGetBeer(personToCheck)) {
    return new Response(BeerCheckStatus.OK, personToCheck.name);
}
return new Response(BeerCheckStatus.NOT_OK, personToCheck.name);
----

== Back to the Main Page

link:../workshops.html[Click here to go back to the main page]
